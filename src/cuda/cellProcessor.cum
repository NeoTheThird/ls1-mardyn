#include <host_defines.h>

#include "util.cum"

#include "cellInfo.cum"

// "concepts" that have to be implemented
#if 0
// general handler
template<class Molecule>
struct MoleculePairHandler {
	void process( int threadIndex, Molecule &moleculeA, Molecule &moleculeB);
};

template<class Molecule>
struct MoleculeDataAccessor {
	// straight-forward getter/setter
	void get(int index, Molecule &molecule);
	void set(int index, const Molecule &molecule);

	// get the molecule data but in a way that merge will be able to merge changes back into the storage
	// (ie. force is set 0 in getMergable, so that merge can add molecule.force to the stored value instead of assigning it
	void getMergable(int index, Molecule &molecule);
	void merge(int index, const Molecule &molecule);
};

template<class Molecule>
struct MoleculeStorage : MoleculeDataAccessor<Molecule> {
};

template<int blockSize>
struct MoleculeLocalStorage : MoleculeDataAccessor<Molecule> {
	void setBlockOffset(uint blockOffset);
	void load(int localIndex, MoleculeDataAccessor<Molecule> &storage, int index);
	void commit(int localIndex, MoleculeDataAccessor<Molecule> &storage, int index);
};

struct CellProcessor {
	void processCellPair(int cellA, int cellB);
	void processCell(int cell);
};
#endif

// actual implementations

// exactly one thread pro block
template<class Molecule, class MoleculeDataAccessor, class MoleculePairHandler>
struct ReferenceCellProcessor {
	MoleculeDataAccessor & __restrict__ moleculeDataAccessor;
	MoleculePairHandler & __restrict__ moleculePairHandler;

	__device__ ReferenceCellProcessor(
			MoleculeDataAccessor & __restrict__ moleculeDataAccessor,
			MoleculePairHandler & __restrict__ moleculePairHandler
		)
		: moleculeDataAccessor( moleculeDataAccessor ), moleculePairHandler( moleculePairHandler ) {}

	__device__ void processCellPair(
			const int threadIndex, const CellInfo & __restrict__ cellA, const CellInfo & __restrict__ cellB
		) {
		for( uint indexA = cellA.startIndex ; indexA < cellA.endIndex ; indexA++ ) {
			Molecule moleculeA;
			moleculeDataAccessor.get( indexA, moleculeA );
			for( uint indexB = cellB.startIndex ; indexB < cellB.endIndex ; indexB++ ) {
				Molecule moleculeB;
				moleculeDataAccessor.get( indexB, moleculeB );

				moleculePairHandler.process( 0, moleculeA, moleculeB );

				moleculeDataAccessor.set( indexB, moleculeB );
			}
			moleculeDataAccessor.set( indexA, moleculeA );
		}
	}

	__device__ void processCell(
			const int threadIndex, const CellInfo & __restrict__ cell
		) {
		for( uint indexA = cell.startIndex ; indexA < cell.endIndex ; indexA++ ) {
			Molecule moleculeA;
			moleculeDataAccessor.get( indexA, moleculeA );
			for( uint indexB = cell.startIndex ; indexB < indexA; indexB++ ) {
				Molecule moleculeB;
				moleculeDataAccessor.get( indexB, moleculeB );

				moleculePairHandler.process( 0, moleculeA, moleculeB );

				moleculeDataAccessor.set( indexB, moleculeB );
			}
			moleculeDataAccessor.set( indexA, moleculeA );
		}
	}
};

template<uint blockSize, uint localStorageBlockSize, class Molecule, class MoleculeStorage, class MoleculeLocalStorage, class MoleculePairHandler>
struct HighDensityCellProcessor {
	MoleculeStorage & __restrict__ moleculeStorage;
	MoleculeLocalStorage & __restrict__ moleculeLocalStorageB;

	MoleculePairHandler & __restrict__ moleculePairHandler;

	__device__ HighDensityCellProcessor(
			MoleculeStorage & __restrict__ moleculeStorage,	MoleculeLocalStorage & __restrict__ moleculeLocalStorage,
			MoleculePairHandler & __restrict__ moleculePairHandler
		)
			: moleculeStorage( moleculeStorage ), moleculeLocalStorageB( moleculeLocalStorage ), moleculePairHandler( moleculePairHandler ) {}

	enum BlockPairType {
		BPT_UNRELATED,
		BPT_SAME_BLOCK   // implies same cell
	};

	__device__ void processMoleculePair(int threadIndex, uint localIndexB, Molecule & __restrict__ moleculeA) {
		Molecule moleculeB;

		moleculeLocalStorageB.get( localIndexB, moleculeB );
		moleculePairHandler.process( threadIndex, moleculeA, moleculeB );
		moleculeLocalStorageB.set( localIndexB, moleculeB );
	}

	// componentLJCenterOffsetFromFirst is only used when blockPairType != BPT_UNRELATED
	template< BlockPairType blockPairType >
	__device__ __noinline__ void processBlock(
			const int threadIndex,
			const uint numMoleculesInBlockA,
			const uint numMoleculesInBlockB,
			const uint blockOffsetB,
			Molecule & __restrict__ moleculeA
			) {

		moleculeLocalStorageB.setBlockOffset( blockOffsetB );

		const uint indexB = blockOffsetB + threadIndex;
		// load B data into cache
		if( threadIndex < numMoleculesInBlockB ) {
			moleculeLocalStorageB.load( threadIndex, moleculeStorage, indexB );
		}

		// make sure all molecules have been written into the cache
		// before any one thread tries to access the cache
		// no __syncthreads() needed here, because at first we work on data from the current warp, so its automatically synched

		const uint localIndexA = threadIndex;

		// process block
#if 1
		/* divide the block into warp blocks
		 * each warp of the thread block (corresponds to a warp block of block A) interacts with
		 * one unique warp block of block B
		 *
		 * if there are more warp blocks in block B than block A,
		 * we need to iterate #warp blocks many times in B
		 *
		 * if there are more warp blocks in block A than block B,
		 * we need to iterate #warp blocks many time in A
		 */
		const uint numWarpBlocks = iceil( max( numMoleculesInBlockA, numMoleculesInBlockB ), WARP_SIZE );
		const uint numWarpBlocksInBlockB = iceil( numMoleculesInBlockB, WARP_SIZE );

		for( uint warpShiftIndex = 0 ; warpShiftIndex < numWarpBlocks ; warpShiftIndex++ ) {
			const uint warpBlockIndexB = (threadIdx.y + warpShiftIndex) % numWarpBlocks;
			const uint warpBlockOffsetB = warpBlockIndexB * WARP_SIZE;

			for( uint moleculeShiftIndex = 0 ; moleculeShiftIndex < WARP_SIZE ; moleculeShiftIndex++ ) {
				const uint localIndexB = warpBlockOffsetB + ((threadIdx.x + moleculeShiftIndex) % WARP_SIZE);

				// only do something when there is a molecule in A and a molecule in B to interact with
				if( localIndexA >= numMoleculesInBlockA || localIndexB >= numMoleculesInBlockB ) {
					continue;
				}

				// if we're not inside the same warp, process all WARP_SIZE * WARP_SIZE pairs inside the warp
				// otherwise only process the lower half
				// indexA >= indexB <=> localIndexA + blockOffsetA >= localIndexB + blockOffsetB <=> localIndexA >= localIndexB
				// (this only works for NUM_WARPS == NUM_LOCAL_STORAGE_WARPS!!!)
				if( blockPairType == BPT_SAME_BLOCK && localIndexB >= localIndexA ) {
					continue;
				}

				processMoleculePair( threadIndex, localIndexB, moleculeA );
			}

			// sync all warps, so that no two different warps will try to access the same warp data block
			__syncthreads();
		}
#else
		uint numMolecules = max( numMoleculesInBlockA, numMoleculesInBlockB );
		for( uint shiftIndex = 0 ; shiftIndex < numMolecules ; shiftIndex++ ) {
			uint localIndexB = (threadIndex + shiftIndex) % numMolecules;

			__syncthreads();

			// only do something when there is a molecule in A and a molecule in B to interact with
			if( localIndexA >= numMoleculesInBlockA || localIndexB >= numMoleculesInBlockB ) {
				continue;
			}

			// if we're not inside the same warp, process all WARP_SIZE * WARP_SIZE pairs inside the warp
			// otherwise only process the lower half
			// indexA >= indexB <=> localIndexA + blockOffsetA >= localIndexB + blockOffsetB <=> localIndexA >= localIndexB
			// (this only works for NUM_WARPS == NUM_LOCAL_STORAGE_WARPS!!!)
			if( blockPairType == BPT_SAME_BLOCK && localIndexB >= localIndexA ) {
				continue;
			}

			processMoleculePair( threadIndex, localIndexB, moleculeA );
		}

		__syncthreads();
#endif

		// write moleculeB back
		if( threadIndex < numMoleculesInBlockB ) {
			moleculeLocalStorageB.commit( threadIndex, moleculeStorage, indexB );
		}
		__syncthreads();
	}

	__device__ void processCellPair( const int threadIndex, const CellInfoEx & cellA, const CellInfoEx & cellB) {
		const uint numBlocksA = iceil<uint>( cellA.length, blockSize );
		const uint numBlocksB = iceil<uint>( cellB.length, localStorageBlockSize );

		for( uint blockIndexA = 0 ; blockIndexA < numBlocksA ; blockIndexA++ ) {
			const uint numMoleculesInBlockA = (blockIndexA < numBlocksA - 1) ? blockSize : shiftedMod( cellA.length, blockSize );
			const uint blockOffsetA = cellA.startIndex + blockIndexA * blockSize;

			Molecule moleculeA;

			const int indexA = blockOffsetA + threadIndex;
			// load A data into (register) cache
			if( threadIndex < numMoleculesInBlockA ) {
				moleculeStorage.get( indexA, moleculeA );
			}

			for( uint blockIndexB = 0 ; blockIndexB < numBlocksB ; blockIndexB++ ) {
				const uint numMoleculesInBlockB = (blockIndexB < numBlocksB - 1) ? localStorageBlockSize : shiftedMod( cellB.length, localStorageBlockSize );
				const uint blockOffsetB = cellB.startIndex + blockIndexB * localStorageBlockSize;

				processBlock<BPT_UNRELATED>(
						threadIndex,
						numMoleculesInBlockA,
						numMoleculesInBlockB,
						blockOffsetB,
						moleculeA
					);
			}

			// push A data back
			if( threadIndex < numMoleculesInBlockA ) {
				moleculeStorage.set(indexA, moleculeA);
			}
		}
	}

	__device__ void processCell(const int threadIndex, const CellInfoEx & __restrict__ cell) {
		const uint numBlocks = iceil<uint>( cell.length, blockSize );

		for( uint blockIndexA = 0 ; blockIndexA < numBlocks ; blockIndexA++ ) {
			const uint numMoleculesInBlockA = (blockIndexA < numBlocks - 1) ? blockSize : shiftedMod( cell.length, blockSize );
			const uint blockOffsetA = cell.startIndex + blockIndexA * blockSize;

			Molecule moleculeA;
			// load A data into (register) cache
			const uint indexA = blockOffsetA + threadIndex;
			if( threadIndex < numMoleculesInBlockA ) {
				moleculeStorage.getMergable( indexA, moleculeA );
			}

			processBlock<BPT_SAME_BLOCK>(
					threadIndex,
					numMoleculesInBlockA,
					numMoleculesInBlockA,
					blockOffsetA,
					moleculeA
				);

			//assert: everything is synced here because of the __syncthreads calls in processBlock
			for( uint blockIndexB = 0 ; blockIndexB < blockIndexA; blockIndexB++ ) {
				// blockIndexB < blockIndexA - 1  < numBlocks - 1 implies numCellsInBlock = BLOCK_SIZE
				const uint numMoleculesInBlockB = blockSize;
				const uint blockOffsetB = cell.startIndex + blockIndexB * blockSize;

				processBlock<BPT_UNRELATED>(
						threadIndex,
						numMoleculesInBlockA,
						numMoleculesInBlockB,
						blockOffsetB,
						moleculeA
					);
			}

			// push A data back
			if( threadIndex < numMoleculesInBlockA ) {
				moleculeStorage.merge( indexA, moleculeA );
			}
		}
	}
};



